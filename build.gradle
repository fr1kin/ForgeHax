import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = project.property('forgehax.version')
group = 'com.matt.forgehax'

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.11.2-13.20.0.2296"
    runDir = "run"

    mappings = "snapshot_20161220"
}

archivesBaseName = "forgehax-" + project.minecraft.version

dependencies {}

jar {
    manifest {
        attributes (
                'FMLCorePlugin': 'com.matt.forgehax.asm.ForgeHaxCoreMod',
                'FMLCorePluginContainsFMLMod': 'true'
        )
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    filter ReplaceTokens, tokens: [
            "forgehax.version": project.property("forgehax.version")
    ]
}

task setGameDir() {
    // Check if custom gamedir has been passed, if not use default ones per platform
    if(!project.hasProperty("gameDir")) {
        if(org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            project.ext.gameDir = System.getenv("APPDATA") + "/.minecraft"
        } else if(org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            project.ext.gameDir = System.properties["user.home"] + "/Library/Application Support/minecraft"
        } else {
            project.ext.gameDir = System.properties["user.home"] + "/.minecraft"
        }
    }
}

task copyJar(type: Copy, dependsOn: [setGameDir]) {
    def gameDirectory = "$gameDir/mods/" + minecraft.version

    // find previous jar (if it exists)
    def previousJars = fileTree(dir: gameDirectory).matching {
        include archivesBaseName + '*'
    }.files

    // change file name
    if(previousJars) {
        // create .backups folder
        def backupDir = gameDirectory + '/.backups/'
        file(backupDir).mkdirs()

        for(File previousJar : previousJars) {
            def n = gameDirectory + '/.backups/' + previousJar.getName() + '.bk'
            def f = file(n)
            def i = 1
            while(f.exists()) {
                f = file(n + '_' + i)
                i++
            }
            previousJar.renameTo(f)
        }
    }

    // copy forgehax jar from libs to forge mod folder
    from jar
    into ("$gameDir/mods/" + minecraft.version)
}

build.finalizedBy copyJar